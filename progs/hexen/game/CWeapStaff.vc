//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class CWeapStaff : ClericWeapon
	__mobjinfo__(10)
	__scriptid__(32);

int BlinkCount;

//==========================================================================
//
//  A_CStaffInitBlink
//
//==========================================================================

final void A_CStaffInitBlink()
{
	BlinkCount = (P_Random() >> 1) + 20;
}

//==========================================================================
//
//  A_CStaffCheckBlink
//
//==========================================================================

final void A_CStaffCheckBlink()
{
	if (!--BlinkCount)
	{
		Owner.Player.SetViewState(ps_weapon, FindState('Blink'));
		BlinkCount = (P_Random() + 50) >> 2;
	}
}

//==========================================================================
//
//  A_CStaffCheck
//
//==========================================================================

final void A_CStaffCheck()
{
	Actor		pmo;
	int			damage;
	int			newLife;
	int			i;
	TAVec		angles;
	TVec		vforward;
	EntityEx	AimTarget;

	pmo = Actor(Owner);
	damage = 20 + (P_Random() & 15);
	AimTarget = pmo.AimEx(vforward, 3.0 * Actor::MELEERANGE / 2.0, 45.0 / 16.0, 3);
	if (AimTarget)
	{
		VectorAngles(&vforward, &angles);
		pmo.LineAttack(vforward, 1.5 * Actor::MELEERANGE, damage, CStaffPuff);
		pmo.Angles.yaw = atan2(AimTarget.Origin.y - pmo.Origin.y,
			AimTarget.Origin.x - pmo.Origin.x);
		if ((AimTarget.bIsPlayer || AimTarget.bMonster) &&
			!AimTarget.bDormant && !AimTarget.bInvulnerable)
		{
			newLife = Owner.Player.Health + (damage >> 3);
			newLife = newLife > 100 ? 100 : newLife;
			pmo.Health = newLife;
			Owner.Player.Health = newLife;
			Owner.Player.SetViewState(ps_weapon, FindState('Fire2'));
		}
		DepleteAmmo(false);
	}
}

//==========================================================================
//
//  A_CStaffAttack
//
//==========================================================================

final void A_CStaffAttack()
{
	EntityEx	mo;
	PlayerPawn	playPawn;

	DepleteAmmo();
	playPawn = PlayerPawn(Owner);
	mo = Actor(Owner).SpawnPlayerMissileAngle(CStaffMissile,
		playPawn.Angles.yaw - (45.0 / 15.0));
	if (mo)
	{
		CStaffMissile(mo).WeaveXY = 32;
	}
	mo = Actor(Owner).SpawnPlayerMissileAngle(CStaffMissile,
		playPawn.Angles.yaw + (45.0 / 15.0));
	if (mo)
	{
		CStaffMissile(mo).WeaveXY = 0;
	}
	playPawn.PlaySound('ClericCStaffFire', CHAN_WEAPON);
}

states
{
Ready:
	CSSF C 4
	CSSF B 3 A_CStaffInitBlink
	CSSF AAAAAAA 1 A_WeaponReady
	CSSF A 1 A_CStaffCheckBlink
	Goto Ready+2
Blink:
	CSSF BBBCCCCCBBB 1 A_WeaponReady
	Goto Ready+2
Deselect:
	CSSF B 3
	CSSF C 4
	CSSF C 1 A_Lower
	Wait
Select:
	CSSF C 1 A_Raise
Fire:
	CSSF A 1 Offset(0, 45) A_CStaffCheck
	CSSF J 1 Offset(0, 50) A_CStaffAttack
	CSSF J 2 Offset(0, 50)
	CSSF J 2 Offset(0, 45)
	CSSF A 2 Offset(0, 40)
	CSSF A 2 Offset(0, 36)
	Goto Ready+2
Fire2:
	CSSF K 10 Offset(0, 36)
	Goto Ready+2
Spawn:
	WCSS A -1
	Stop
}

defaultproperties
{
	WeaponClass = HexenDefs::PCLASS_CLERIC;
	Slot = 2;
	SelectionOrder = 1600;
	AmmoType1 = Mana1;
	AmmoUse1 = 1;
	AmmoGive1 = 25;
	PSpriteSY = 10.0;
	PickupMessage = "$txt_weapon_c2";
	BotCombatDist = 192.0;
	bBotProjectile = true;
}
