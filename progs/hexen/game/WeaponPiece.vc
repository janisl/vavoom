//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class WeaponPiece : Pickup
	abstract;

const int
	WPIECE1			= 1,
	WPIECE2			= 2,
	WPIECE3			= 4;

int PieceClass;
int PieceValue;

//==========================================================================
//
//  TouchSpecial
//
//==========================================================================

void TouchSpecial(Actor Toucher)
{
	bool remove;
	bool checkAssembled;
	bool gaveWeapon;
	int gaveMana;

	if (!Toucher.bPickUp)
	{
		// Can't be picked up by toucher
		return;
	}
	if (Toucher.Health <= 0)
	{
		// Toucher is dead
		return;
	}
	remove = true;
	checkAssembled = true;
	gaveWeapon = false;
	if (Toucher.Player.PClass != PieceClass)
	{
		// Wrong class, but try to pick up for mana
		if (Level.Game.netgame && !Level.Game.deathmatch)
		{
			// Can't pick up wrong-class weapons in coop netplay
			return;
		}
		checkAssembled = false;
		gaveMana = Player(Toucher.Player).GiveMana(HexenDefs::MANA_BOTH, 20);
		if (!gaveMana)
		{
			// Didn't need the mana, so don't pick it up
			return;
		}
	}
	else if (Level.Game.netgame && !Level.Game.deathmatch)
	{
		// Cooperative net-game
		if (Player(Toucher.Player).Pieces & PieceValue)
		{
			// Already has the piece
			return;
		}
		PieceValue = MainGameInfo(Level.Game).pieceValueTrans[PieceValue];
		Player(Toucher.Player).GiveMana(HexenDefs::MANA_BOTH, 20);
		remove = false;
	}
	else
	{
		// Deathmatch or single player game
		gaveMana = Player(Toucher.Player).GiveMana(HexenDefs::MANA_BOTH, 20);
		if (Player(Toucher.Player).Pieces & PieceValue)
		{
			// Already has the piece, check if mana needed
			if (!gaveMana)
			{
				// Didn't need the mana, so don't pick it up
				return;
			}
			checkAssembled = false;
		}
	}

	// Pick up the weapon piece
	if (Special)
	{
		Level.ExecuteActionSpecial(Special, Args[0], Args[1], Args[2], Args[3],
			Args[4], NULL, 0, Toucher);
		Special = 0;
	}
	if (remove)
	{
		if (Level.Game.deathmatch && !bDropped)
		{
			HideSpecial();
		}
		else
		{
			Remove();
		}
	}
	Player(Toucher.Player).BonusFlash += BONUSADD;

	// Check if fourth weapon assembled
	if (checkAssembled)
	{
		Player(Toucher.Player).Pieces |= PieceValue;
		if (Player(Toucher.Player).Pieces == (WPIECE1 | WPIECE2 | WPIECE3))
		{
			gaveWeapon = true;
			Player(Toucher.Player).WeaponOwned[HexenDefs::WP_FOURTH] = true;
			Player(Toucher.Player).PendingWeapon = HexenDefs::WP_FOURTH;
		}
	}

	if (gaveWeapon)
	{
		Toucher.Player.cprint(MainGameInfo(Level.Game).fourthWeaponText[PieceClass]);
		// Play the build-sound full volume for all players
		Toucher.PlayFullVolumeSound('WeaponBuild', CHAN_ITEM);
	}
	else
	{
		Toucher.Player.cprint(MainGameInfo(Level.Game).weaponPieceText[PieceClass]);
		Toucher.PlayFullVolumeSound('PickupWeapon', CHAN_ITEM);	//FIXME must be PickupPiece
	}
}

defaultproperties
{
	bFloatBob = true;
}
