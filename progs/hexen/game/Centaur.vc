//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Centaur : Pawn
	__mobjinfo__(107)
	__scriptid__(1);

//==========================================================================
//
//	GetReflectedAngle
//
//==========================================================================

float GetReflectedAngle(Actor Other)
{
	float angle = atan2(Other.Origin.y - Origin.y,
		Other.Origin.x - Origin.x);

	// Change angle for delflection/reflection
	if (fabs(AngleMod180(angle - Angles.yaw)) > 45.0 * 45.0 / 32.0)
	{
		return -1.0;
	}
	if (HolyFX(Other))
	{
		return -1.0;
	}
	// Deflection
	if (Random() < 0.5)
		angle = AngleMod360(angle + 45.0);
	else
		angle = AngleMod360(angle - 45.0);
	return angle;
}

//==========================================================================
//
//  A_CentaurAttack
//
//==========================================================================

final void A_CentaurAttack()
{
	if (!Enemy)
	{
		return;
	}
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, P_Random() % 7 + 3);
	}
}

//==========================================================================
//
//	A_SetReflective
//
//==========================================================================

final void A_SetReflective()
{
	bReflective = true;

	A_SetInvulnerable();
}

//==========================================================================
//
//	A_UnSetReflective
//
//==========================================================================

final void A_UnSetReflective()
{
	bReflective = false;

	A_UnSetInvulnerable();
}

//==========================================================================
//
//	A_CentaurDefend
//
//==========================================================================

final void A_CentaurDefend()
{
	A_FaceTarget();
	if (CheckMeleeRange() && Random() < 0.125)
	{
		A_UnSetInvulnerable();
		SetState(MeleeState);
	}
}

//==========================================================================
//
//  A_CentaurDropStuff
//
//  Spawn shield/sword sprites when the centaur pulps
//
//==========================================================================

final void A_CentaurDropStuff()
{
	Actor A;
	float angle;

	A = Spawn(CentaurShield, Origin + vector(0.0, 0.0, 45.0));
	if (A)
	{
		angle = AngleMod360(Angles.yaw + 90.0);
		A.Velocity.x = ((Random() - 0.5) * 8.0 + 1.0) * cos(angle) * 35.0;
		A.Velocity.y = ((Random() - 0.5) * 8.0 + 1.0) * sin(angle) * 35.0;
		A.Velocity.z = (8.0 + Random() * 4.0) * 35.0;
		A.Instigator = self;
	}
	A = Spawn(CentaurSword, Origin + vector(0.0, 0.0, 45.0));
	if (A)
	{
		angle = AngleMod360(Angles.yaw - 90.0);
		A.Velocity.x = ((Random() - 0.5) * 8.0 + 1.0) * cos(angle) * 35.0;
		A.Velocity.y = ((Random() - 0.5) * 8.0 + 1.0) * sin(angle) * 35.0;
		A.Velocity.z = (8.0 + Random() * 4.0) * 35.0;
		A.Instigator = self;
	}
}

__states__
{
	S_CENTAUR_LOOK1('CENT', 0, 10.0 / 35.0, S_CENTAUR_LOOK2) = A_Look
	S_CENTAUR_LOOK2('CENT', 1, 10.0 / 35.0, S_CENTAUR_LOOK1) = A_Look
	S_CENTAUR_WALK1('CENT', 0, 4.0 / 35.0, S_CENTAUR_WALK2) = A_Chase
	S_CENTAUR_WALK2('CENT', 1, 4.0 / 35.0, S_CENTAUR_WALK3) = A_Chase
	S_CENTAUR_WALK3('CENT', 2, 4.0 / 35.0, S_CENTAUR_WALK4) = A_Chase
	S_CENTAUR_WALK4('CENT', 3, 4.0 / 35.0, S_CENTAUR_WALK1) = A_Chase
	S_CENTAUR_ATK1('CENT', 7, 5.0 / 35.0, S_CENTAUR_ATK2) = A_FaceTarget
	S_CENTAUR_ATK2('CENT', 8, 4.0 / 35.0, S_CENTAUR_ATK3) = A_FaceTarget
	S_CENTAUR_ATK3('CENT', 9, 7.0 / 35.0, S_CENTAUR_WALK1) = A_CentaurAttack
	S_CENTAUR_PAIN1('CENT', 6, 6.0 / 35.0, S_CENTAUR_PAIN2) = A_Pain
	S_CENTAUR_PAIN2('CENT', 6, 6.0 / 35.0, S_CENTAUR_PAIN3) = A_SetReflective
	S_CENTAUR_PAIN3('CENT', 4, 15.0 / 35.0, S_CENTAUR_PAIN4) = A_CentaurDefend
	S_CENTAUR_PAIN4('CENT', 4, 15.0 / 35.0, S_CENTAUR_PAIN5) = A_CentaurDefend
	S_CENTAUR_PAIN5('CENT', 4, 15.0 / 35.0, S_CENTAUR_PAIN6) = A_CentaurDefend
	S_CENTAUR_PAIN6('CENT', 4, 1.0 / 35.0, S_CENTAUR_WALK1) = A_UnSetReflective
	S_CENTAUR_DEATH1('CENT', 10, 4.0 / 35.0, S_CENTAUR_DEATH2) = none
	S_CENTAUR_DEATH2('CENT', 11, 4.0 / 35.0, S_CENTAUR_DEATH3) = A_Scream
	S_CENTAUR_DEATH3('CENT', 12, 4.0 / 35.0, S_CENTAUR_DEATH4) = none
	S_CENTAUR_DEATH4('CENT', 13, 4.0 / 35.0, S_CENTAUR_DEATH5) = none
	S_CENTAUR_DEATH5('CENT', 14, 4.0 / 35.0, S_CENTAUR_DEATH6) = A_NoBlocking
	S_CENTAUR_DEATH6('CENT', 15, 4.0 / 35.0, S_CENTAUR_DEATH7) = none
	S_CENTAUR_DEATH7('CENT', 16, 4.0 / 35.0, S_CENTAUR_DEATH8) = none
	S_CENTAUR_DEATH8('CENT', 17, 4.0 / 35.0, S_CENTAUR_DEATH9) = A_QueueCorpse
	S_CENTAUR_DEATH9('CENT', 18, 4.0 / 35.0, S_CENTAUR_DEATH0) = none
	S_CENTAUR_DEATH0('CENT', 19, -1.0, none) = none
	S_CENTAUR_DEATH_X1('CTXD', 0, 4.0 / 35.0, S_CENTAUR_DEATH_X2) = none
	S_CENTAUR_DEATH_X2('CTXD', 1, 4.0 / 35.0, S_CENTAUR_DEATH_X3) = A_NoBlocking
	S_CENTAUR_DEATH_X3('CTXD', 2, 4.0 / 35.0, S_CENTAUR_DEATH_X4) = A_CentaurDropStuff
	S_CENTAUR_DEATH_X4('CTXD', 3, 3.0 / 35.0, S_CENTAUR_DEATH_X5) = A_Scream
	S_CENTAUR_DEATH_X5('CTXD', 4, 4.0 / 35.0, S_CENTAUR_DEATH_X6) = A_QueueCorpse
	S_CENTAUR_DEATH_X6('CTXD', 5, 3.0 / 35.0, S_CENTAUR_DEATH_X7) = none
	S_CENTAUR_DEATH_X7('CTXD', 6, 4.0 / 35.0, S_CENTAUR_DEATH_X8) = none
	S_CENTAUR_DEATH_X8('CTXD', 7, 3.0 / 35.0, S_CENTAUR_DEATH_X9) = none
	S_CENTAUR_DEATH_X9('CTXD', 8, 4.0 / 35.0, S_CENTAUR_DEATH_X10) = none
	S_CENTAUR_DEATH_X10('CTXD', 9, 3.0 / 35.0, S_CENTAUR_DEATH_X11) = none
	S_CENTAUR_DEATH_X11('CTXD', 10, -1.0, none) = none
	S_CENTAUR_ICE('CENT', 20, 5.0 / 35.0, S_CENTAUR_ICE2) = A_FreezeDeath
	S_CENTAUR_ICE2('CENT', 20, 1.0 / 35.0, S_CENTAUR_ICE2) = A_FreezeDeathChunks
}

defaultproperties
{
	Health = 200;
	GibsHealth = -100;
	Radius = 20.0;
	Height = 64.0;
	Mass = 120.0;
	StepSpeed = 13.0;
	PainChance = 0.52734375;
	IdleState = S_CENTAUR_LOOK1;
	SeeState = S_CENTAUR_WALK1;
	MeleeState = S_CENTAUR_ATK1;
	PainState = S_CENTAUR_PAIN1;
	DeathState = S_CENTAUR_DEATH1;
	GibsDeathState = S_CENTAUR_DEATH_X1;
	IceDeathState = S_CENTAUR_ICE;
	SightSound = 'CentaurSight';
	ActiveSound = 'CentaurActive';
	AttackSound = 'CentaurAttack';
	PainSound = 'CentaurPain';
	DeathSound = 'CentaurDeath';
}
