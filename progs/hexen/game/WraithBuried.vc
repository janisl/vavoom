//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class WraithBuried:Wraith
	__mobjinfo__(10011);

//==========================================================================
//
//  A_WraithRaiseInit
//
//==========================================================================

void A_WraithRaiseInit(void)
{
	Translucency = 0;
	bNonShootable = false;
	bShootable = true;
	bSolid = true;
	FloorClip = Height;
}

//============================================================================
//
//  RaiseFromFloor
//
//  Raise incrementally from the floor
// 
//============================================================================

bool RaiseFromFloor(void)
{
	bool done;

	done = true;
	// Raise a mobj from the ground
	if (FloorClip > 0.0)
	{
		FloorClip -= 2.0;
		if (FloorClip <= 0.0)
		{
			FloorClip = 0.0;
			done = true;
		}
		else
		{
			done = false;
		}
	}
	return done;	// Reached target height
}

//==========================================================================
//
//  A_WraithRaise
//
//==========================================================================

void A_WraithRaise(void)
{
	if (RaiseFromFloor())
	{
		// Reached it's target height
		SetState(S_WRAITH_CHASE1);
	}

	SpawnDirt(Radius);
}

__states__
{
	S_WRAITH_RAISE1('WRTH', 0, 2.0 / 35.0, S_WRAITH_RAISE2) { A_WraithRaiseInit(); }
	S_WRAITH_RAISE2('WRTH', 0, 2.0 / 35.0, S_WRAITH_RAISE3) { A_WraithRaise(); }
	S_WRAITH_RAISE3('WRTH', 0, 2.0 / 35.0, S_WRAITH_RAISE4) { A_FaceTarget(); }
	S_WRAITH_RAISE4('WRTH', 1, 2.0 / 35.0, S_WRAITH_RAISE5) { A_WraithRaise(); }
	S_WRAITH_RAISE5('WRTH', 1, 2.0 / 35.0, S_WRAITH_RAISE2) { A_WraithRaise(); }
}

defaultproperties
{
	Height = 68.0;
	bSolid = false;
	Translucency = 100;
	bShootable = false;
	IdleState = S_WRAITH_LOOK1;
	SeeState = S_WRAITH_RAISE1;
}

//**************************************************************************
//
//  $Log$
//  Revision 1.1  2002/10/26 16:32:27  dj_jl
//  New style of defining classes.
//
//**************************************************************************
