//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Pickup : Inventory
	abstract;

string			PickupMessage;
name			PickupSound;

bool			bFullVolPickupSound;
bool			bFancyPickup;
bool			bDormant2Min;
bool			bDormant10Min;

const float BONUSADD		= 0.2;

//==========================================================================
//
//  AngleIncrements
//
//==========================================================================

int AngleIncrements()
{
	return bMonster ? 45 : 1;
}

//==========================================================================
//
//  TouchSpecial
//
//==========================================================================

final void TouchSpecial(EntityEx Toucher)
{
	if (!Toucher.bPickUp)
	{
		// Can't be picked up by toucher
		return;
	}
	if (Toucher.Health <= 0)
	{
		// Toucher is dead
		return;
	}

	if (!TryPickup(Toucher))
	{
		return;
	}

	Toucher.Player.cprint(GetPickupMessage());
	if (Special)
	{
		Level.ExecuteActionSpecial(Special, Args[0], Args[1], Args[2], Args[3],
			Args[4], NULL, 0, Toucher);
		Special = 0;
	}
	if (bDropped || !ShouldStay())
	{
		HidePickup();
	}
	Player(Toucher.Player).BonusFlash += BONUSADD;
	PlayPickupSound(Toucher);
}

//==========================================================================
//
//	TryPickup
//
//==========================================================================

bool TryPickup(EntityEx Toucher)
{
	return false;
}

//==========================================================================
//
//	GetPickupMessage
//
//==========================================================================

string GetPickupMessage()
{
	return PickupMessage;
}

//==========================================================================
//
//	HidePickup
//
//==========================================================================

final void HidePickup()
{
	if (bFancyPickup)
	{
		//  Removes the bSpecial flag and initiates the artifact pickup animation.
		bSpecial = false;
		if (ShouldRespawn() && !bDropped)
		{
			if (bDormant2Min)
			{
				SetState(S_DORMANTARTI2_1);
			}
			else if (bDormant10Min)
			{
				SetState(S_DORMANTARTI3_1);
			}
			else
			{
				SetState(S_DORMANTARTI1_1);
			}
		}
		else
		{
			// Don't respawn
			SetState(S_DEADARTI1);
		}
	}
	else
	{
		if (ShouldRespawn() && !bDropped)
		{
			HideSpecial();
		}
		else
		{
			Destroy();
		}
	}
}

//==========================================================================
//
//	PlayPickupSound
//
//==========================================================================

void PlayPickupSound(EntityEx Toucher)
{
	if (PickupSound)
	{
		Toucher.PlaySound(PickupSound, CHAN_ITEM, 1.0,
			bFullVolPickupSound ? ATTN_NONE : ATTN_NORMAL);
	}
}

//==========================================================================
//
//	ShouldRespawn
//
//==========================================================================

bool ShouldRespawn()
{
	return Level.Game.deathmatch;
}

//==========================================================================
//
//  HideSpecial
//
//==========================================================================

final void HideSpecial()
{
	bSpecial = false;
	bHidden = true;
	SetState(S_HIDESPECIAL1);
}

//==========================================================================
//
//  A_HideThing
//
//==========================================================================

final void A_HideThing()
{
	bHidden = true;
}

//==========================================================================
//
//  A_UnHideThing
//
//==========================================================================

final void A_UnHideThing()
{
	bHidden = false;
}

//==========================================================================
//
//  A_RestoreSpecialThing1
//
//  Make a special thing visible again.
//
//==========================================================================

final void A_RestoreSpecialThing1()
{
	bHidden = false;
	PlaySound('Respawn', CHAN_VOICE);
}

//==========================================================================
//
//  A_RestoreSpecialThing2
//
//==========================================================================

final void A_RestoreSpecialThing2()
{
	bSpecial = true;
	SetState(IdleState);
}

//==========================================================================
//
//  A_RestoreArtifact
//
//==========================================================================

final void A_RestoreArtifact()
{
	bSpecial = true;
	SetState(IdleState);
	PlaySound('Respawn', CHAN_VOICE);
}

__states__
{
	//	Hide for 40 secs
	S_HIDESPECIAL1('ACLO', 4, 1400.0 / 35.0, S_HIDESPECIAL2) = none
	S_HIDESPECIAL2('ACLO', 0, 4.0 / 35.0, S_HIDESPECIAL3) = A_RestoreSpecialThing1
	S_HIDESPECIAL3('ACLO', 1, 4.0 / 35.0, S_HIDESPECIAL4) = none
	S_HIDESPECIAL4('ACLO', 0, 4.0 / 35.0, S_HIDESPECIAL5) = none
	S_HIDESPECIAL5('ACLO', 1, 4.0 / 35.0, S_HIDESPECIAL6) = none
	S_HIDESPECIAL6('ACLO', 2, 4.0 / 35.0, S_HIDESPECIAL7) = none
	S_HIDESPECIAL7('ACLO', 1, 4.0 / 35.0, S_HIDESPECIAL8) = none
	S_HIDESPECIAL8('ACLO', 2, 4.0 / 35.0, S_HIDESPECIAL9) = none
	S_HIDESPECIAL9('ACLO', 3, 4.0 / 35.0, S_HIDESPECIAL10) = none
	S_HIDESPECIAL10('ACLO', 2, 4.0 / 35.0, S_HIDESPECIAL11) = none
	S_HIDESPECIAL11('ACLO', 3, 4.0 / 35.0, none) = A_RestoreSpecialThing2

	//	Dormant for 40 secs
	S_DORMANTARTI1_1('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI1_2) = none
	S_DORMANTARTI1_2('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI1_3) = none
	S_DORMANTARTI1_3('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI1_4) = none
	S_DORMANTARTI1_4('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI1_5) = none
	S_DORMANTARTI1_5('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI1_6) = none
	S_DORMANTARTI1_6('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI1_7) = none
	S_DORMANTARTI1_7('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI1_8) = none
	S_DORMANTARTI1_8('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI1_9) = none
	S_DORMANTARTI1_9('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI1_10) = none
	S_DORMANTARTI1_10('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI1_11) = none
	S_DORMANTARTI1_11('ACLO', 0, 1400.0 / 35.0, S_DORMANTARTI1_12) = A_HideThing
	S_DORMANTARTI1_12('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI1_13) = A_UnHideThing
	S_DORMANTARTI1_13('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI1_14) = none
	S_DORMANTARTI1_14('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI1_15) = none
	S_DORMANTARTI1_15('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI1_16) = none
	S_DORMANTARTI1_16('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI1_17) = none
	S_DORMANTARTI1_17('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI1_18) = none
	S_DORMANTARTI1_18('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI1_19) = none
	S_DORMANTARTI1_19('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI1_20) = none
	S_DORMANTARTI1_20('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI1_21) = none
	S_DORMANTARTI1_21('ACLO', 3, 3.0 / 35.0, none) = A_RestoreArtifact

	//	Dormant for 2 minutes
	S_DORMANTARTI2_1('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI2_2) = none
	S_DORMANTARTI2_2('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI2_3) = none
	S_DORMANTARTI2_3('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI2_4) = none
	S_DORMANTARTI2_4('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI2_5) = none
	S_DORMANTARTI2_5('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI2_6) = none
	S_DORMANTARTI2_6('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI2_7) = none
	S_DORMANTARTI2_7('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI2_8) = none
	S_DORMANTARTI2_8('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI2_9) = none
	S_DORMANTARTI2_9('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI2_10) = none
	S_DORMANTARTI2_10('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI2_11) = none
	S_DORMANTARTI2_11('ACLO', 0, 4200.0 / 35.0, S_DORMANTARTI2_12) = A_HideThing
	S_DORMANTARTI2_12('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI2_13) = A_UnHideThing
	S_DORMANTARTI2_13('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI2_14) = none
	S_DORMANTARTI2_14('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI2_15) = none
	S_DORMANTARTI2_15('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI2_16) = none
	S_DORMANTARTI2_16('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI2_17) = none
	S_DORMANTARTI2_17('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI2_18) = none
	S_DORMANTARTI2_18('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI2_19) = none
	S_DORMANTARTI2_19('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI2_20) = none
	S_DORMANTARTI2_20('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI2_21) = none
	S_DORMANTARTI2_21('ACLO', 3, 3.0 / 35.0, none) = A_RestoreArtifact

	//	Dormant for 10 minutes
	S_DORMANTARTI3_1('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI3_2) = none
	S_DORMANTARTI3_2('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI3_3) = none
	S_DORMANTARTI3_3('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI3_4) = none
	S_DORMANTARTI3_4('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI3_5) = none
	S_DORMANTARTI3_5('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI3_6) = none
	S_DORMANTARTI3_6('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI3_7) = none
	S_DORMANTARTI3_7('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI3_8) = none
	S_DORMANTARTI3_8('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI3_9) = none
	S_DORMANTARTI3_9('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI3_10) = none
	S_DORMANTARTI3_10('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI3_11) = none
	S_DORMANTARTI3_11('ACLO', 0, 21000.0 / 35.0, S_DORMANTARTI3_12) = A_HideThing
	S_DORMANTARTI3_12('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI3_13) = A_UnHideThing
	S_DORMANTARTI3_13('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI3_14) = none
	S_DORMANTARTI3_14('ACLO', 0, 3.0 / 35.0, S_DORMANTARTI3_15) = none
	S_DORMANTARTI3_15('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI3_16) = none
	S_DORMANTARTI3_16('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI3_17) = none
	S_DORMANTARTI3_17('ACLO', 1, 3.0 / 35.0, S_DORMANTARTI3_18) = none
	S_DORMANTARTI3_18('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI3_19) = none
	S_DORMANTARTI3_19('ACLO', 3, 3.0 / 35.0, S_DORMANTARTI3_20) = none
	S_DORMANTARTI3_20('ACLO', 2, 3.0 / 35.0, S_DORMANTARTI3_21) = none
	S_DORMANTARTI3_21('ACLO', 3, 3.0 / 35.0, none) = A_RestoreArtifact

	S_DEADARTI1('ACLO', 3, 3.0 / 35.0, S_DEADARTI2) = none
	S_DEADARTI2('ACLO', 2, 3.0 / 35.0, S_DEADARTI3) = none
	S_DEADARTI3('ACLO', 3, 3.0 / 35.0, S_DEADARTI4) = none
	S_DEADARTI4('ACLO', 2, 3.0 / 35.0, S_DEADARTI5) = none
	S_DEADARTI5('ACLO', 1, 3.0 / 35.0, S_DEADARTI6) = none
	S_DEADARTI6('ACLO', 2, 3.0 / 35.0, S_DEADARTI7) = none
	S_DEADARTI7('ACLO', 1, 3.0 / 35.0, S_DEADARTI8) = none
	S_DEADARTI8('ACLO', 0, 3.0 / 35.0, S_DEADARTI9) = none
	S_DEADARTI9('ACLO', 1, 3.0 / 35.0, S_DEADARTI10) = none
	S_DEADARTI10('ACLO', 0, 3.0 / 35.0, none) = none
}

defaultproperties
{
	Height = 32.0;
	bSpecial = true;
	bNoPassMobj = true;
	PickupSound = 'PickupItem';
}
