//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class MWeapFrost : MageWeapon
	__mobjinfo__(53)
	__scriptid__(36);

//==========================================================================
//
//  A_FireConePL1
//
//==========================================================================

final void A_FireConePL1()
{
	TAVec			ang;
	int				damage;
	int				i;
	FrostMissile	mo;
	int				conedone = false;
	TVec			Dir;
	EntityEx		AimTarget;

	if (!bIsPlayer)
	{
		return;
	}
	Weapon Wpn = PlayerEx(Player).ReadyWeapon;

	Wpn.DepleteAmmo();
	PlaySound('MageShardsFire', CHAN_WEAPON);

	damage = 90 + (P_Random() & 15);
	for (i = 0; i < 16; i++)
	{
		ang = Angles;
		ang.yaw = AngleMod360(ang.yaw + itof(i) * (45.0 / 16.0));
		AimTarget = AimLineAttack(Dir, ang, MELEERANGE);
		if (AimTarget)
		{
			AimTarget.Damage(self, self, damage, 'Ice');
			conedone = true;
			break;
		}
	}

	// didn't find any creatures, so fire projectiles
	if (!conedone)
	{
		mo = FrostMissile(SpawnPlayerMissile(FrostMissile));
		if (mo)
		{
			mo.bShardSpawnLeft = true;
			mo.bShardSpawnRight = true;
			mo.bShardSpawnUp = true;
			mo.bShardSpawnDown = true;
			mo.Special2 = 3;	// Set sperm count (levels of reproductivity)
			mo.Target = self;
			mo.Args[0] = 3;	// Mark Initial shard as super damage
		}
	}
}

states
{
Ready:
	CONE A 1 A_WeaponReady
	Loop
Deselect:
	CONE A 1 A_Lower
	Loop
Select:
	CONE A 1 A_Raise
	Loop
Fire:
	CONE B 3
	CONE C 4
Hold:
	CONE D 3
	CONE E 5
	CONE F 3 A_FireConePL1
	CONE G 3
	CONE A 9
	CONE A 10 A_ReFire
	Goto Ready
Spawn:
	WMCS ABC 8 Bright
	Loop
}

defaultproperties
{
	WeaponClass = HexenDefs::PCLASS_MAGE;
	Slot = 2;
	SelectionOrder = 1700;
	AmmoType1 = Mana1;
	AmmoUse1 = 3;
	AmmoGive1 = 25;
	PSpriteSY = 20.0;
	PickupMessage = "$txt_weapon_m2";
	BotCombatDist = 256.0;
	bBotProjectile = true;
}
