//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class IntermissionScreen : Window;

ClientGameBase ClGame;

const float TALLY_EFFECT_TIME	= 0.625;
const float TALLY_FINAL_X_DELTA	= 23.0;
const float TALLY_FINAL_Y_DELTA	= 13.0;
const float TALLY_START_XPOS	= 338.0;
const float TALLY_STOP_XPOS		= 250.0;
const float TALLY_START_YPOS	= 272.0;
const float TALLY_STOP_YPOS		= 223.0;
const int TALLY_TOP_X			= 245;
const int TALLY_TOP_Y			= 149;
const int TALLY_LEFT_X			= 167;
const int TALLY_LEFT_Y			= 211;
const int TALLY_TOTALS_X		= 451;

float TextSpeed;
float TextWait;

// specifies current state
int interstate;
float intertime;

// background (map of levels).
int patchINTERPIC;

float HubCount;
string HubText;

// used for general timing
float cnt;

int slaughterboy;
int totalFrags[MAXPLAYERS];

int tallytop;
int tallylft;

bool showTotals;

name ClusMsgLumpNames[5];

//==========================================================================
//
//  Start
//
//==========================================================================

void Start()
{
	IM_InitStats();
	IM_LoadPics();
	interstate = 0;
	ClGame.skipintermission = false;
	intertime = 0.0;

	if (!ClGame.cl.deathmatch && !HubCount)
	{
		interstate = 1;
		cnt = 0.3;
	}
}

//==========================================================================
//
//	Tick
//
//==========================================================================

void Tick(float DeltaTime)
{
	if (interstate)
	{
		if (cnt > 0.0)
		{
			cnt -= DeltaTime;
			if (cnt <= 0.0)
			{
				IM_UnloadPics();
				CmdBuf_AddText("TeleportNewMap\n");
			}
		}
		return;
	}
	intertime += DeltaTime;
	if (ClGame.skipintermission || (!ClGame.cl.deathmatch && !HubCount))
	{
		interstate = 1;
		cnt = 0.3;
		ClGame.skipintermission = false;
	}
}

//==========================================================================
//
//  DrawWindow
//
//==========================================================================

void DrawWindow(GC gc)
{
	if (interstate)
	{
		return;
	}

	gc.DrawFullScreenPic(patchINTERPIC);

	if (!ClGame.cl.deathmatch)
	{
		if (HubCount)
		{
			IM_DrawHubText();
		}
	}
	else
	{
		IM_DrawDeathTally();
	}
}

//========================================================================
//
//  IM_LoadPics
//
//========================================================================

void IM_LoadPics()
{
	if (HubCount || ClGame.cl.deathmatch)
	{
		patchINTERPIC = R_RegisterPic('interpic');
		tallytop = R_RegisterPic('tallytop');
		tallylft = R_RegisterPic('tallylft');
	}
}

//========================================================================
//
//  IM_UnloadPics
//
//========================================================================

void IM_UnloadPics()
{
}

//========================================================================
//
//  IM_InitStats
//
//  Initializes the stats for single player mode
//
//========================================================================

void IM_InitStats()
{
	int i;
	int j;
	int slaughterfrags;
	int posnum;
	int slaughtercount;
	int playercount;
	name msgLumpName;

	if (!ClGame.cl.deathmatch)
	{
		HubCount = 0.0;
		if (ClGame.im->leavecluster != ClGame.im->entercluster)
		{
			if (ClGame.im->leavecluster >= 1 && ClGame.im->leavecluster <= 5)
			{
				msgLumpName = ClusMsgLumpNames[ClGame.im->leavecluster - 1];
				HubText = LoadTextLump(msgLumpName);
				HubCount = itof(strlen(HubText)) * TextSpeed + TextWait;
#ifdef FIXME
				S_StartSong("hub", P_GetCDIntermissionTrack(), true);
#else
				CmdBuf_AddText("music loop hub\n");
#endif
			}
		}
	}
	else
	{
		slaughterboy = 0;
		slaughterfrags = -9999;
		posnum = 0;
		playercount = 0;
		slaughtercount = 0;
		for (i = 0; i < MAXPLAYERS; i++)
		{
			totalFrags[i] = 0;
			if (ClGame.scores[i].bActive)
			{
				playercount++;
				for (j = 0; j < MAXPLAYERS; j++)
				{
					if (ClGame.scores[j].bActive)
					{
						totalFrags[i] += ClGame.scores[i].frags[j];
					}
				}
				posnum++;
			}
			if (totalFrags[i] > slaughterfrags)
			{
				slaughterboy = 1 << i;
				slaughterfrags = totalFrags[i];
				slaughtercount = 1;
			}
			else if (totalFrags[i] == slaughterfrags)
			{
				slaughterboy |= 1 << i;
				slaughtercount++;
			}
		}
		if (playercount == slaughtercount)
		{	// don't do the slaughter stuff if everyone is equal
			slaughterboy = 0;
		}
#ifdef FIXME
		S_StartSong("hub", P_GetCDIntermissionTrack(), true);
#else
		CmdBuf_AddText("music loop hub\n");
#endif
	}
}

//===========================================================================
//
//  IM_DrawHubText
//
//===========================================================================

void IM_DrawHubText()
{
	int count;

	count = ftoi((intertime - 0.3) / TextSpeed);
	if (count < 0)
	{
		count = 0;
	}
	if (count > strlen(HubText))
	{
		count = strlen(HubText);
	}
	T_SetFont(font_small);
	T_SetAlign(hleft, vtop);
	T_DrawText(170, 145, substr(HubText, 0, count));
}

//==========================================================================
//
//  IM_DrawNumber
//
//==========================================================================

void IM_DrawNumber(int val, int x, int y, int wrapThresh)
{
	T_SetFont(font_small);
	T_SetAlign(hcenter, vtop);
	if (!(val < -9 && wrapThresh < 1000))
	{
		T_DrawText(x, y, va("%d",
				val >= wrapThresh ? val % wrapThresh : val));
	}
	else
	{
		T_DrawText(x, y, "XX");
	}
}

//==========================================================================
//
//  IM_DrawNumberBold
//
//==========================================================================

void IM_DrawNumberBold(int val, int x, int y, int wrapThresh)
{
	T_SetFont(font_yellow);
	T_SetAlign(hcenter, vtop);
	if (!(val < -9 && wrapThresh < 1000))
	{
		T_DrawText(x, y, va("%d",
				val >= wrapThresh ? val % wrapThresh : val));
	}
	else
	{
		T_DrawText(x, y, "XX");
	}
}

//========================================================================
//
//  IM_DrawDeathTally
//
//========================================================================

void IM_DrawDeathTally()
{
	int i, j;
	float xPos, yPos;
	float xDelta, yDelta;
	float xStart, scale;
	int x, y;
	bool bold;

	R_DrawPic(TALLY_TOP_X, TALLY_TOP_Y, tallytop);
	R_DrawPic(TALLY_LEFT_X, TALLY_LEFT_Y, tallylft);
	if (intertime < TALLY_EFFECT_TIME)
	{
		showTotals = false;
		scale = intertime / TALLY_EFFECT_TIME;
		xDelta = scale * TALLY_FINAL_X_DELTA;
		yDelta = scale * TALLY_FINAL_Y_DELTA;
		xStart = TALLY_START_XPOS -
			scale * (TALLY_START_XPOS - TALLY_STOP_XPOS);
		yPos = TALLY_START_YPOS -
			scale * (TALLY_START_YPOS - TALLY_STOP_YPOS);
	}
	else
	{
		xDelta = TALLY_FINAL_X_DELTA;
		yDelta = TALLY_FINAL_Y_DELTA;
		xStart = TALLY_STOP_XPOS;
		yPos = TALLY_STOP_YPOS;
	}
	if (intertime >= TALLY_EFFECT_TIME && showTotals == false)
	{
		showTotals = true;
		LocalSound('PlatformStop');
	}
	y = ftoi(yPos);
	for (i = 0; i < MAXPLAYERS; i++)
	{
		xPos = xStart;
		for (j = 0; j < MAXPLAYERS; j++, xPos += xDelta)
		{
			x = ftoi(xPos);
			bold = (i == ClGame.cl.clientnum || j == ClGame.cl.clientnum);
			if (ClGame.scores[i].bActive && ClGame.scores[j].bActive)
			{
				if (bold)
				{
					IM_DrawNumberBold(ClGame.scores[i].frags[j], x, y, 100);
				}
				else
				{
					IM_DrawNumber(ClGame.scores[i].frags[j], x, y, 100);
				}
			}
			else
			{
				if (bold)
				{
					T_SetFont(font_yellow);
				}
				else
				{
					T_SetFont(font_small);
				}
				T_SetAlign(hcenter, vtop);
				T_DrawText(x, y, "--");
			}
		}
		if (showTotals && ClGame.scores[i].bActive && !((slaughterboy & (1 << i)) &&
				!(ftoi(32.0 * intertime) & 16)))
		{
			IM_DrawNumber(totalFrags[i], TALLY_TOTALS_X, y, 1000);
		}
		yPos += yDelta;
		y = ftoi(yPos);
	}
}

//==========================================================================
//
//	VisibilityChanged
//
//==========================================================================

void VisibilityChanged(bool bNewVisibility)
{
	bTickEnabled = bNewVisibility;
}

defaultproperties
{
	bTickEnabled = true;
	Width = 640;
	Height = 480;
	TextSpeed = 0.1;
	TextWait = 4.0;
	ClusMsgLumpNames[0] = 'clus1msg';
	ClusMsgLumpNames[1] = 'clus2msg';
	ClusMsgLumpNames[2] = 'clus3msg';
	ClusMsgLumpNames[3] = 'clus4msg';
	ClusMsgLumpNames[4] = 'clus5msg';
}
