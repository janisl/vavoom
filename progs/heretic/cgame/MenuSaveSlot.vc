//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ı
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class MenuSaveSlot:MenuChoice;

int Status;
bool bEnteringText;
int SlotNum;
int Text[(SLOTTEXTLEN + 4) / 4];
int EnterText[(SLOTTEXTLEN + 4) / 4];

void InitWindow(void)
{
	::InitWindow();
	InitSlot();
}

void DrawWindow(GC gc)
{
	if (bEnteringText && bSelected)
	{
		DrawSlot(gc, 0, ARR2STR(EnterText), true);
	}
	else
	{
		DrawSlot(gc, 0, ARR2STR(Text), false);
	}
}

void ActivateSlot(void)
{
}

bool Key(int key)
{
	int Len;
	
	if (bEnteringText)
	{
		// Save Game string input
		switch (key)
		{
		case K_ESCAPE:
		case K_MOUSE2:
			bEnteringText = false;
			break;

		case K_ENTER:
		case K_PADENTER:
		case K_MOUSE1:
			bEnteringText = false;
			if (strlen(ARR2STR(EnterText)))
			{
				strcpy(ARR2STR(Text), ARR2STR(EnterText));
				CmdBuf_AddText("Save %d \"%s\"\n", SlotNum,
					ARR2STR(Text));
				GetClGame().ForceMenuOff();
			}
			break;

    	case K_BACKSPACE:
		    if (strlen(ARR2STR(EnterText)))
			{
				strsetchar(ARR2STR(EnterText),
 					strlen(ARR2STR(EnterText)) - 1, 0);
			}
			break;
			
		default:
		    if (key >= 32 && key < 128)
			{
  				key = TranslateKey(key);
				Len = strlen(ARR2STR(EnterText));
			    if (Len < SLOTTEXTLEN - 1)
    			{
					strsetchar(ARR2STR(EnterText), Len++, key);
					strsetchar(ARR2STR(EnterText), Len, 0);
		    	}
			}
			break;
		}
		return true;
	}

	if (key == K_ENTER || key == K_PADENTER ||
		key == K_MOUSE1 || key == K_JOY1)
	{
		ActivateSlot();
		return true;
	}
	return false;
}

defaultproperties
{
}

//**************************************************************************
//
//  $Log$
//  Revision 1.3  2006/02/09 22:33:10  dj_jl
//  Moved all client game code to classes.
//
//  Revision 1.2  2003/03/08 12:13:51  dj_jl
//  Menu code cleanup.
//
//  Revision 1.1  2002/10/26 16:32:22  dj_jl
//  New style of defining classes.
//
//**************************************************************************
