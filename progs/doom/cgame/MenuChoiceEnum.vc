//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class MenuChoiceEnum : MenuChoice;

const int MAX_ENUM_CHOICES = 32;

string Text;
bool bLeftAlign;

int Value;
int MaxValue;
string EnumNames[MAX_ENUM_CHOICES];

delegate bool ValueChanging(Object Sender, int NewValue);

//==========================================================================
//
//  SetOrigin
//
//==========================================================================

void SetOrigin(int x, int y)
{
	if (bLeftAlign)
	{
		SetPos(x, y);
	}
	else
	{
		SetPos(x - 160, y);
		CursorXOffs = 160;
	}
}

bool SetNewValue(int newValue)
{
	if (ValueChanging)
	{
		return ValueChanging(self, newValue);
	}
	return true;
}

void AddItem(string AValue)
{
	EnumNames[MaxValue] = AValue;
	MaxValue++;
}

void SetInitialValue(int AValue)
{
	Value = AValue;
	if (Value < 0)
		Value = 0;
	if (Value >= MaxValue)
		Value = MaxValue - 1;
}

void OnCreate()
{
	int i;

	::OnCreate();

	MaxValue = 0;
	for (i = 0; i < 32; i++)
	{
		if (EnumNames[i])
		{
			MaxValue++;
		}
	}
}

void OnDraw()
{
	int xx;

	if (bLeftAlign)
	{
		SetTextAlign(hleft, vtop);
		DrawText(0, 0, Text);
	}
	else
	{
		SetTextAlign(hright, vtop);
		DrawText(160, 0, Text);
	}

	if (bLeftAlign)
	{
		xx = TextWidth(Text) + 8;
	}
	else
	{
		xx = 160 + 10;
	}
	SetTextAlign(hleft, vtop);
	DrawText(xx, 0, EnumNames[Value]);
}

bool OnKeyDown(int key)
{
	int newValue;

	switch (key)
	{
	case K_LEFTARROW:
		if (Value > 0)
			newValue = Value - 1;
		else
			newValue = MaxValue - 1;
		if (SetNewValue(newValue))
		{
			Value = newValue;
			LocalSound('menu/change');
		}
		return true;
	case K_RIGHTARROW:
	case K_ENTER:
	case K_PADENTER:
	case K_MOUSE1:
		if (Value < MaxValue - 1)
			newValue = Value + 1;
		else
			newValue = 0;
		if (SetNewValue(newValue))
		{
			Value = newValue;
			LocalSound('menu/change');
		}
		return true;
	}
	return false;
}

defaultproperties
{
	Height = 10;
}
