//------------------------------------------------------------------------------
actor Revenant 66
{
	game Doom
	SpawnID 20
	Health 300
	Radius 20
	Height 56
	Mass 500
	Speed 10
	PainChance 100
	MeleeThreshold 196
	SeeSound "skeleton/sight"
	ActiveSound "skeleton/active"
	PainSound "skeleton/pain"
	DeathSound "skeleton/death"
	Obituary "$ob_undead"
	HitObituary "$ob_undeadhit"
	Monster
	+MissileMore
	+FloorClip
	states
	{
	Spawn:
		SKEL AB 10 A_Look
		Loop
	See:
		SKEL AABBCCDDEEFF 2 A_Chase
		Loop
	Melee:
		SKEL G 0 A_FaceTarget
		SKEL G 6 A_SkelWhoosh
		SKEL H 6 A_FaceTarget
		SKEL I 6 A_SkelFist
		Goto See
	Missile:
		SKEL J 0 Bright A_FaceTarget
		SKEL J 10 Bright A_FaceTarget
		SKEL K 10 A_SkelMissile
		SKEL K 10 A_FaceTarget
		Goto See
	Pain:
		SKEL L 5
		SKEL L 5 A_Pain
		Goto See
	Death:
		SKEL LM 7
		SKEL N 7 A_Scream
		SKEL O 7 A_NoBlocking
		SKEL P 7
		SKEL Q -1
		Stop
	Raise:
		SKEL QPONML 5
		Goto See
	}
}

//------------------------------------------------------------------------------
actor RevenantTracer
{
	game Doom
	SpawnID 53
	Radius 11
	Height 8
	Speed 10
	Damage 10
	RenderStyle Add
	SeeSound "skeleton/attack"
	DeathSound "skeleton/tracex"
	Projectile
	+SeekerMissile
	+Randomize
	states
	{
	Spawn:
		FATB AB 2 Bright A_Tracer
		Loop
	Death:
		FBXP A 8 Bright
		FBXP B 6 Bright
		FBXP C 4 Bright
		Stop
	}
}

//------------------------------------------------------------------------------
actor RevenantTracerSmoke
{
	RenderStyle Translucent
	Alpha 0.666
	+NoBlockmap
	+NoGravity
	+NoTeleport
	states
	{
	Spawn:
		PUFF BCBCD 4
		Stop
	}
}
