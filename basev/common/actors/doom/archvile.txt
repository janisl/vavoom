//------------------------------------------------------------------------------
actor Archvile 64
{
	game Doom
	SpawnID 111
	Health 700
	Radius 20
	Height 56
	Mass 500
	Speed 15
	PainChance 10
	MaxTargetRange 896
	SeeSound "vile/sight"
	ActiveSound "vile/active"
	PainSound "vile/pain"
	DeathSound "vile/death"
	Obituary "$ob_vile"
	Monster
	+QuickToRetaliate
	+NoTarget
	+FloorClip
	states
	{
	Spawn:
		VILE AB 10 A_Look
		Loop
	See:
		VILE AABBCCDDEEFF 2 A_VileChase
		Loop
	Missile:
		VILE G 0 Bright A_VileStart
		VILE G 10 Bright A_FaceTarget
		VILE H 8 Bright A_VileTarget
		VILE IJKLMN 8 Bright A_FaceTarget
		VILE O 8 Bright A_VileAttack
		VILE P 20 Bright
		Goto See
	Heal:
		VILE "[\]" 10 Bright
		Goto See
	Pain:
		VILE Q 5
		VILE Q 5 A_Pain
		Goto See
	Death:
		VILE Q 7
		VILE R 7 A_Scream
		VILE S 7 A_NoBlocking
		VILE TUVW 7
		VILE XY 5
		VILE Z -1
		Stop
	}
}

//------------------------------------------------------------------------------
actor ArchvileFire
{
	game Doom
	SpawnID 98
	RenderStyle Add
	Alpha 1
	+NoBlockmap
	+NoGravity
	states
	{
	Spawn:
		FIRE A 2 Bright A_StartFire
		FIRE BAB 2 Bright A_Fire
		FIRE C 2 Bright A_FireCrackle
		FIRE BCBCDCDCDEDED 2 Bright A_Fire
		FIRE E 2 Bright A_FireCrackle
		FIRE FEFEFGHGHGH 2 Bright A_Fire
		Stop
	}
}
