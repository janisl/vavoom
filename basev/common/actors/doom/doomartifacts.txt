//------------------------------------------------------------------------------
actor Soulsphere : Health 2013
{
	Game Doom
	SpawnID 25
	RenderStyle Translucent
	Alpha 0.666
	Inventory.Amount 100
	Inventory.MaxAmount 200
	Inventory.PickupMessage "$gotsuper"
	Inventory.PickupSound "misc/p_pkup"
	+CountItem
	+Inventory.AlwaysPickup
	states
	{
	Spawn:
		SOUL ABCDCB 6 Bright
		Loop
	}
}

//------------------------------------------------------------------------------
actor InvulnerabilitySphere : PowerupGiver 2022
{
	Game Doom
	SpawnID 133
	RenderStyle Translucent
	Alpha 0.666
	Inventory.PickupMessage "$gotinvul"
	Inventory.MaxAmount 0
	Powerup.Type Invulnerable
	Powerup.Color InverseMap
	+CountItem
	+Inventory.BigPowerup
	+Inventory.AutoActivate
	+Inventory.AlwaysPickup
	states
	{
	Spawn:
		PINV ABCD 6 Bright
		Loop
	}
}

//------------------------------------------------------------------------------
actor BlurSphere : PowerupGiver 2024
{
	Game Doom
	SpawnID 135
	RenderStyle Translucent
	Alpha 0.666
	Inventory.PickupMessage "$gotinvis"
	Inventory.MaxAmount 0
	Powerup.Type Invisibility
	+CountItem
	+Inventory.BigPowerup
	+Inventory.AutoActivate
	+Inventory.AlwaysPickup
	states
	{
	Spawn:
		PINS ABCD 6 Bright
		Loop
	}
}

//------------------------------------------------------------------------------
actor RadSuit : PowerupGiver 2025
{
	Game Doom
	SpawnID 136
	Inventory.PickupMessage "$gotsuit"
	Inventory.MaxAmount 0
	Powerup.Type IronFeet
	+Inventory.AutoActivate
	+Inventory.AlwaysPickup
	states
	{
	Spawn:
		SUIT A -1 Bright
		Stop
	}
}

//------------------------------------------------------------------------------
actor Infrared : PowerupGiver 2045
{
	Game Doom
	SpawnID 138
	Inventory.MaxAmount 0
	Inventory.PickupMessage "$gotvisor"
	Powerup.Type LightAmp
	+CountItem
	+Inventory.AutoActivate
	+Inventory.AlwaysPickup
	states
	{
	Spawn:
		PVIS A 6 Bright
		PVIS B 6
		Loop
	}
}

//------------------------------------------------------------------------------
actor Allmap : MapRevealer 2026
{
	Game Doom
	SpawnID 137
	Inventory.PickupMessage "$gotmap"
	Inventory.PickupSound "misc/p_pkup"
	+CountItem
	+Inventory.AlwaysPickup
	states
	{
	Spawn:
		PMAP ABCDCB 6 Bright
		Loop
	}
}

//------------------------------------------------------------------------------
actor MegasphereHealth : Health
{
	Inventory.Amount 200
	Inventory.MaxAmount 200
	+Inventory.AlwaysPickup
}

//------------------------------------------------------------------------------
actor Megasphere : CustomInventory 83
{
	Game Doom
	SpawnID 132
	RenderStyle Translucent
	Alpha 0.666
	Inventory.PickupMessage "$gotmsphere"
	Inventory.PickupSound "misc/p_pkup"
	+CountItem
	+Inventory.AlwaysPickup
	states
	{
	Spawn:
		MEGA ABCD 6 Bright
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("BlueArmor")
		TNT1 A 0 A_GiveInventory("MegasphereHealth")
		Stop
	}
}

//------------------------------------------------------------------------------
actor Berserk : CustomInventory 2023
{
	Game Doom
	SpawnID 134
	Inventory.PickupSound "misc/p_pkup"
	Inventory.PickupMessage "$gotberserk"
	+CountItem
	+Inventory.AlwaysPickup
	states
	{
	Spawn:
		PSTR A -1 Bright
		Stop
	Pickup:
		TNT1 A 0 A_GiveInventory("PowerStrength")
		TNT1 A 0 HealThing(100)
		TNT1 A 0 A_SelectWeapon("Fist")
		Stop
	}
}
