//------------------------------------------------------------------------------
actor ZombieMan 3004
{
	game Doom
	SpawnID 4
	Health 20
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	SeeSound "grunt/sight"
	ActiveSound "grunt/active"
	AttackSound "grunt/attack"
	PainSound "grunt/pain"
	DeathSound "grunt/death1"
	Obituary "$ob_zombie"
	DropItem "Clip"
	Monster
	+FloorClip
	states
	{
	Spawn:
		POSS AB 10 A_Look
		Loop
	See:
		POSS AABBCCDD 4 A_Chase
		Loop
	Missile:
		POSS E 10 A_FaceTarget
		POSS F 8 A_PosAttack
		POSS E 8
		Goto See
	Pain:
		POSS G 3
		POSS G 3 A_Pain
		Goto See
	Death:
		POSS H 5
		POSS I 5 A_Scream
		POSS J 5 A_NoBlocking
		POSS K 5
		POSS L -1
		Stop
	XDeath:
		POSS M 5
		POSS N 5 A_XScream
		POSS O 5 A_NoBlocking
		POSS PQRST 5
		POSS U -1
		Stop
	Raise:
		POSS KJIH 5
		Goto See
	}
}

//------------------------------------------------------------------------------
actor ShotgunGuy 9
{
	game Doom
	SpawnID 1
	Health 30
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	SeeSound "shotguy/sight"
	ActiveSound "shotguy/active"
	AttackSound "shotguy/attack"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	Obituary "$ob_shotguy"
	DropItem "Shotgun"
	Monster
	+FloorClip
	states
	{
	Spawn:
		SPOS AB 10 A_Look
		Loop
	See:
		SPOS AABBCCDD 3 A_Chase
		Loop
	Missile:
		SPOS E 10 A_FaceTarget
		SPOS F 10 Bright A_SPosAttackUseAtkSound
		SPOS E 10
		Goto See
	Pain:
		SPOS G 3
		SPOS G 3 A_Pain
		Goto See
	Death:
		SPOS H 5
		SPOS I 5 A_Scream
		SPOS J 5 A_NoBlocking
		SPOS K 5
		SPOS L -1
		Stop
	XDeath:
		SPOS M 5
		SPOS N 5 A_XScream
		SPOS O 5 A_NoBlocking
		SPOS PQRST 5
		SPOS U -1
		Stop
	Raise:
		SPOS LKJIH 5
		Goto See
	}
}

//------------------------------------------------------------------------------
actor ChaingunGuy 65
{
	game Doom
	SpawnID 2
	Health 70
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	SeeSound "chainguy/sight"
	ActiveSound "chainguy/active"
	AttackSound "chainguy/attack"
	PainSound "chainguy/pain"
	DeathSound "chainguy/death"
	Obituary "$ob_chainguy"
	DropItem "Chaingun"
	Monster
	+FloorClip
	states
	{
	Spawn:
		CPOS AB 10 A_Look
		Loop
	See:
		CPOS AABBCCDD 3 A_Chase
		Loop
	Missile:
		CPOS E 10 A_FaceTarget
		CPOS FE 4 Bright A_CPosAttack
		CPOS F 1 A_CPosRefire
		Goto Missile+1
	Pain:
		CPOS G 3
		CPOS G 3 A_Pain
		Goto See
	Death:
		CPOS H 5
		CPOS I 5 A_Scream
		CPOS J 5 A_NoBlocking
		CPOS KLM 5
		CPOS N -1
		Stop
	XDeath:
		CPOS O 5
		CPOS P 5 A_XScream
		CPOS Q 5 A_NoBlocking
		CPOS RS 5
		CPOS T -1
		Stop
	Raise:
		CPOS NMLKJIH 5
		Goto See
	}
}

//------------------------------------------------------------------------------
actor WolfensteinSS 84
{
	game Doom
	SpawnID 116
	Health 50
	Radius 20
	Height 56
	Speed 8
	PainChance 170
	SeeSound "wolfss/sight"
	ActiveSound "wolfss/active"
	AttackSound "wolfss/attack"
	PainSound "wolfss/pain"
	DeathSound "wolfss/death"
	Obituary "$ob_wolfss"
	DropItem "Clip"
	Monster
	+FloorClip
	states
	{
	Spawn:
		SSWV AB 10 A_Look
		Loop
	See:
		SSWV AABBCCDD 3 A_Chase
		Loop
	Missile:
		SSWV EF 10 A_FaceTarget
		SSWV G 4 Bright A_CPosAttack
		SSWV F 6 A_FaceTarget
		SSWV G 4 Bright A_CPosAttack
		SSWV F 1 A_CPosRefire
		Goto Missile+1
	Pain:
		SSWV H 3
		SSWV H 3 A_Pain
		Goto See
	Death:
		SSWV I 5
		SSWV J 5 A_Scream
		SSWV K 5 A_NoBlocking
		SSWV L 5
		SSWV M -1
		Stop
	XDeath:
		SSWV N 5
		SSWV O 5 A_XScream
		SSWV P 5 A_NoBlocking
		SSWV QRSTU 5
		SSWV V -1
		Stop
	Raise:
		SSWV MLKJI 5
		Goto See
	}
}
