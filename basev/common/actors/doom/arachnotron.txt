//------------------------------------------------------------------------------
actor Arachnotron 68
{
	game Doom
	SpawnID 6
	Health 500
	Radius 64
	Height 64
	Mass 600
	Speed 12
	PainChance 128
	SeeSound "baby/sight"
	ActiveSound "baby/active"
	PainSound "baby/pain"
	DeathSound "baby/death"
	Obituary "$ob_baby"
	Monster
	+FloorClip
	+BossDeath
	states
	{
	Spawn:
		BSPI AB 10 A_Look
		Loop
	See:
		BSPI A 20
		BSPI A 3 A_BabyMetal
		BSPI ABBCC 3 A_Chase
		BSPI D 3 A_BabyMetal
		BSPI DEEFF 3 A_Chase
		Goto See+1
	Missile:
		BSPI A 20 Bright A_FaceTarget
		BSPI G 4 Bright A_BspiAttack
		BSPI H 4 Bright
		BSPI H 1 Bright A_SpidRefire
		Goto Missile+1
	Pain:
		BSPI I 3
		BSPI I 3 A_Pain
		Goto See+1
	Death:
		BSPI J 20 A_Scream
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7
		BSPI P -1 A_BossDeath
		Stop
	Raise:
		BSPI PONMLKJ 5
		Goto See
	}
}

//------------------------------------------------------------------------------
actor ArachnotronPlasma
{
	game Doom
	SpawnID 129
	Radius 13
	Height 8
	Speed 25
	Damage 5
	RenderStyle Add
	Alpha 0.75
	SeeSound "baby/attack"
	DeathSound "baby/shotx"
	Projectile
	+Randomize
	states
	{
	Spawn:
		APLS AB 5 Bright
		Loop
	Death:
		APBX ABCDE 5 Bright
		Stop
	}
}
