//------------------------------------------------------------------------------
actor PainElemental 71
{
	game Doom
	SpawnID 115
	Health 400
	Radius 31
	Height 56
	Mass 400
	Speed 8
	PainChance 128
	SeeSound "pain/sight"
	ActiveSound "pain/active"
	PainSound "pain/pain"
	DeathSound "pain/death"
	Monster
	+NoGravity
	+Float
	states
	{
	Spawn:
		PAIN A 10 A_Look
		Loop
	See:
		PAIN AABBCC 3 A_Chase
		Loop
	Missile:
		PAIN DE 5 A_FaceTarget
		PAIN F 5 Bright A_FaceTarget
		PAIN F 0 Bright A_PainAttack
		Goto See
	Pain:
		PAIN G 6
		PAIN G 6 A_Pain
		Goto See
	Death:
		PAIN H 8 Bright
		PAIN I 8 Bright A_Scream
		PAIN JK 8 Bright
		PAIN L 8 Bright A_PainDie
		PAIN M 8 Bright
		Stop
	Raise:
		PAIN MLKJIH 8
		Goto See
	}
}
