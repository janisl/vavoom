//------------------------------------------------------------------------------
actor FighterPlayer : PlayerPawn
{
	Health 100
	Radius 16
	Height 64
	PainChance 255
	RadiusDamageFactor 0.25
	PainSound "PlayerFighterPain"
	Player.ViewHeight 48
	Player.ForwardMove 1.08, 1.2
	Player.SideMove 1.125, 1.475
	Player.ColorRange 246, 254
	Player.HexenArmor 15, 25, 20, 15, 5
	Player.JumpZ 9
	Player.DisplayName "Fighter"
	Player.HealRadiusType "Armor"
	Player.SoundClass "fighter"
	Player.SpawnClass "Fighter"
	Player.ScoreIcon "fiteface"
	Player.StartItem "FWeapFist"
	states
	{
	Spawn:
		PLAY A -1
		Stop
	See:
		PLAY ABCD 4
		Loop
	Melee:
	Missile:
		PLAY EF 8
		Goto Spawn
	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
		Goto Spawn
	Death:
		PLAY H 6
		PLAY I 6 A_PlayerScream
		PLAY JK 6
		PLAY L 6 A_NoBlocking
		PLAY M 6
		PLAY N -1
		Stop
	XDeath:
		PLAY O 5 A_PlayerScream
		PLAY P 5 A_SkullPop("HexenBloodySkull")
		PLAY R 5 A_NoBlocking
		PLAY STUV 5
		PLAY W -1
		Stop
	Ice:
		PLAY X 5 A_FreezeDeath
		PLAY X 1 A_FreezeDeathChunks
		Wait
	Burn:
		FDTH A 5 Bright A_PlaySound("*burndeath")
		FDTH B 4 Bright
		FDTH G 5 Bright
		FDTH H 4 Bright A_PlayerScream
		FDTH I 5 Bright
		FDTH J 4 Bright
		FDTH K 5 Bright
		FDTH L 4 Bright
		FDTH M 5 Bright
		FDTH N 4 Bright
		FDTH O 5 Bright
		FDTH P 4 Bright
		FDTH Q 5 Bright
		FDTH R 4 Bright
		FDTH S 5 Bright A_NoBlocking
		FDTH T 4 Bright
		FDTH U 5 Bright
		FDTH V 4 Bright
		ACLO E 35 A_CheckPlayerDone
		Wait
		ACLO E 8
		Stop
	}
}

//------------------------------------------------------------------------------
actor HexenBloodySkull : PlayerChunk
{
	Radius 4
	Height 4
	Gravity 0.125
	+NoBlockmap
	+CanPass
	+CannotPush
	states
	{
	Spawn:
		BSKL ABCDFGH 5 A_CheckFloor("Hit")
		Loop
	Hit:
		BSKL I 0 A_PlaySound("Drip")
		BSKL I 16 A_CheckPlayerDone
		Wait
	}
}
