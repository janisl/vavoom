//------------------------------------------------------------------------------
actor FWeaponPiece1 : FighterWeaponPiece 12
{
	Game Hexen
	SpawnID 29
	+FloatBob
	Inventory.PickupMessage "$txt_quietus_piece"
	WeaponPiece.Number 1
	WeaponPiece.Weapon "FWeapQuietus"
	states
	{
	Spawn:
		WFR1 A -1 Bright
		Stop
	}
}

//------------------------------------------------------------------------------
actor FWeaponPiece2 : FWeaponPiece1 13
{
	Game Hexen
	SpawnID 30
	WeaponPiece.Number 2
	states
	{
	Spawn:
		WFR2 A -1 Bright
		Stop
	}
}

//------------------------------------------------------------------------------
actor FWeaponPiece3 : FWeaponPiece1 16
{
	Game Hexen
	SpawnID 31
	WeaponPiece.Number 3
	states
	{
	Spawn:
		WFR3 A -1 Bright
		Stop
	}
}

//------------------------------------------------------------------------------
actor FWeapQuietus : FighterWeapon
{
	+ExtremeDeath
	Inventory.PickupMessage "$txt_weapon_f4"
	Inventory.PickupSound "WeaponBuild"
	Weapon.SelectionOrder 2900
	Weapon.AmmoType1 "Mana1"
	Weapon.AmmoType2 "Mana2"
	Weapon.AmmoUse1 14
	Weapon.AmmoUse2 14
	Weapon.AmmoGive1 20
	Weapon.AmmoGive2 20
	Weapon.YAdjust 10
	Weapon.BotCombatDist 256
	+Weapon.Explosive
	+Weapon.Primary_Uses_Both

	action native A_FSwordAttack();

	states
	{
	Ready:
		FSRD AAAABBBBCCCC 1 Bright A_WeaponReady
		Loop
	Deselect:
		FSRD A 1 Bright A_Lower
		Loop
	Select:
		FSRD A 1 Bright A_Raise
		Loop
	Fire:
		FSRD D 3 Bright Offset(5, 36)
		FSRD E 3 Bright Offset(5, 36)
		FSRD F 2 Bright Offset(5, 36)
		FSRD G 3 Bright Offset(5, 36) A_FSwordAttack
		FSRD H 2 Bright Offset(5, 36)
		FSRD I 2 Bright Offset(5, 36)
		FSRD I 10 Bright Offset(5, 150)
		FSRD A 1 Bright Offset(5, 60)
		FSRD B 1 Bright Offset(5, 55)
		FSRD C 1 Bright Offset(5, 50)
		FSRD A 1 Bright Offset(5, 45)
		FSRD B 1 Bright Offset(5, 40)
		Goto Ready
	}
}

//------------------------------------------------------------------------------
actor FSwordFlame
{
	RenderStyle Translucent
	Alpha 0.6
	+NoBlockmap
	+NoGravity
	states
	{
	Spawn:
		FSFX NOPQRSTUVW 3 Bright
		Stop
	}
}
